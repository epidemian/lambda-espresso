assert = require 'assert'
{Var, App, Fun, Def, parse, termStr, reduceProgram} = require '../src/lambda'

parseTerm = (str) ->
  {terms, defs} = parse str
  assert.equal 1, terms.length
  assert.deepEqual {}, defs
  terms[0]

assertParse = (str, expected) ->
  term = parseTerm str
  assert.deepEqual term, expected

describe 'parse()', ->
  it 'parses variables', ->
    assertParse 'x', Var 'x'

  it 'parses almost anything as a variable', ->
    assertParse 'Y', Var 'Y'
    assertParse '42', Var '42'
    assertParse "optimus'", Var "optimus'"
    assertParse "WAT!?", Var "WAT!?"
    assertParse 'Ã±Â²', Var 'Ã±Â²'
    assertParse "~>", Var "~>"
    assertParse 'ðŸ’©', Var 'ðŸ’©'

  it 'parses applications', ->
    assertParse 'a b', App (Var 'a'), (Var 'b')

  it 'parses lambda abstractions', ->
    assertParse 'Î»x.x', (Fun 'x', (Var 'x'))

  it 'ignores whitespace', ->
    assertParse '  x  ', Var 'x'
    assertParse '  x  y  ', (App (Var 'x'), (Var 'y'))
    assertParse '  Î»x  .  x  ', (Fun 'x', (Var 'x'))

  it 'ignores comments', ->
    assertParse '''
      ; This is a comment
      x ; The variable x
    ''', Var 'x'

  it 'ignores unnecessary parentheses', ->
    assertParse '(x)', Var 'x'
    assertParse '(((x)))', Var 'x'

    assertParse '(x y)', (App (Var 'x'), (Var 'y'))
    assertParse '(x) y', (App (Var 'x'), (Var 'y'))
    assertParse 'x (y)', (App (Var 'x'), (Var 'y'))

    assertParse '(Î»x.x)', (Fun 'x', (Var 'x'))
    assertParse 'Î»x.(x)', (Fun 'x', (Var 'x'))

  it 'parses nested applications', ->
    assertParse 'x y z',     (App (App (Var 'x'), (Var 'y')), Var 'z')
    assertParse '((x) y) z', (App (App (Var 'x'), (Var 'y')), Var 'z')
    assertParse 'x (y (z))', (App (Var 'x'), (App (Var 'y'), (Var 'z')))

  it 'parses nested abstractions', ->
    assertParse 'Î»x.Î»y.Î»z.w',     (Fun 'x', (Fun 'y', (Fun 'z', Var 'w')))
    assertParse 'Î»x.(Î»y.(Î»z.w))', (Fun 'x', (Fun 'y', (Fun 'z', Var 'w')))

  it 'parses definitions', ->
    {defs, terms} = parse '''
      id = Î»x.x
      id y
    '''

    assert.deepEqual ['id'], (Object.keys defs)
    assert.deepEqual defs.id, (Fun 'x', (Var 'x'))

    assert.equal 1, terms.length
    assert.deepEqual terms[0], (App (Def 'id', (Fun 'x', (Var 'x'))), Var 'y')

  it 'allows using a definition before it is declared', ->
    {defs, terms} = parse '''
      id y
      id = Î»x.x
    '''

    assert.deepEqual ['id'], (Object.keys defs)
    assert.deepEqual defs.id, (Fun 'x', (Var 'x'))

    assert.equal 1, terms.length
    assert.deepEqual terms[0], (App (Def 'id', (Fun 'x', (Var 'x'))), Var 'y')

  it 'allows a definition referencing other definition(s)', ->
    {defs, terms} = parse '''
      id = Î»x.x
      id2 = id id
    '''
    id = Fun 'x', (Var 'x')

    assert.deepEqual ['id', 'id2'], (Object.keys defs)
    assert.deepEqual defs.id, id
    assert.deepEqual defs.id2, (App (Def 'id', id), (Def 'id', id))

    assert.equal 0, terms.length

  it 'disallows redefinitions', ->
    assert.throws (->
      parse '''
        foo = Î»x.x
        bar = Î»y.y
        foo = Î»z.z
      '''),
      /^Error: foo already defined$/

  it 'disallows recursive definitions', ->
    assert.throws (-> parse 'walk = (Î»x.x) walk'),
      /^Error: Illegal recursive reference in "walk"\./

  it 'disallows mutually-recursive definitions', ->
    assert.throws (->
      parse '''
        foo = (Î»x.x) bar
        bar = baz qux
        baz = qux foo
        qux = Î»x.x
      '''),
      /^Error: Illegal recursive reference in "baz"\..*baz â†’ foo â†’ bar â†’ baz/

  it 'disallows free variables on definitions', ->
    assert.throws (->
      parse '''
        foo = Î»x.x bar
      '''),
      /^Error: Illegal free variable "bar" in "foo"\./

  it 'does not confuse definitions with bound variables', ->
    {defs, terms} = parse '''
      x = Î»x.x
      Î»y.x Î»x.x y
    '''

    assert.equal 1, terms.length
    assert.deepEqual terms[0], (Fun 'y', (
      App (Def 'x', (Fun 'x', (Var 'x'))), (Fun 'x', (App (Var 'x'), (Var 'y')))
    ))

  it 'parses a whole program' # TODO program with more than one def and term


assertTermStr = (str, expected) ->
  term = parseTerm str
  assert.equal (termStr term), expected

describe 'termStr()', ->
  it 'respects operations\' precedence when applying parentheses', ->
    assertTermStr 'Î»a.a Î»b.b Î»c.c Î»d.d', 'Î»a.a Î»b.b Î»c.c Î»d.d'
    assertTermStr '(Î»a.a) (Î»b.b) (Î»c.c) Î»d.d', '(Î»a.a) (Î»b.b) (Î»c.c) Î»d.d'
    assertTermStr '(((Î»a.a) Î»b.b) Î»c.c) Î»d.d', '(Î»a.a) (Î»b.b) (Î»c.c) Î»d.d'
    assertTermStr 'Î»a.a (Î»b.b (Î»c.c (Î»d.d)))', 'Î»a.a Î»b.b Î»c.c Î»d.d'

reduceTerm = (str, options) ->
  reductions = reduceProgram str, options
  assert.equal 1, reductions.length
  reductions[0]

assertReduce = (expr, expected) ->
  assert.strictEqual (reduceTerm expr).final, expected

describe 'reduceProgram()', ->

  it 'does not reduce simple irreducible expressions', ->
    assertReduce 'x', 'x'
    assertReduce 'x y', 'x y'
    assertReduce 'Î»x.x', 'Î»x.x'

  it 'reduces simple beta-redexes', ->
    assertReduce '(Î»x.x) y', 'y'
    assertReduce '(Î»x.x y) z', 'z y'
    assertReduce '(Î»x.x y) y', 'y y'
    assertReduce '(Î»x.x x) y', 'y y'

  it 'reduces multiple beta-redex', ->
    assertReduce '(Î»x.Î»y.x y) v w', 'v w'

  it 'reduces a beta-redex that is formed only after a reduction', ->
    assertReduce '(Î»x.x y) (Î»z.z z)', 'y y'

  it 'does at most maxStep reduction steps', ->
    {totalSteps} = reduceTerm '(Î»x.x x) (Î»x.x x)', maxSteps: 42
    assert totalSteps is 42

  it 'indicates when a reduction terminates', ->
    {terminates} = reduceTerm '(Î»x.x) y'
    assert terminates

  it 'indicates when a reduction does not terminate', ->
    {terminates} = reduceTerm '(Î»x.x x) (Î»x.x x)'
    assert not terminates

  describe 'renaming in substitution (Î»y.T)[x := S]', ->

    it 'renames when y is free in S and x is free in T', ->
      assertReduce '(Î»x.Î»y.x y) (y z)', 'Î»y1.y z y1'

    it 'does not rename if y does not occur free in S but x is free in T', ->
      assertReduce '(Î»x.Î»y.x y) (w z)', 'Î»y.w z y'

    it 'does not rename if y is free in S but x does not occur in T', ->
      assertReduce '(Î»x.Î»y.y) (y z)', 'Î»y.y'

    it 'does not rename if y is free in S but x has only bound occurrences in T', ->
      assertReduce '(Î»x.Î»y.y Î»x.x) (y z)', 'Î»y.y Î»x.x'
      assertReduce '(Î»x.Î»y.y Î»x.x x y Î»x.x) (y z)', 'Î»y.y Î»x.x x y Î»x.x'

    it 'does not choose a name that makes an inner variable bind to another abstraction', ->
      # In this case, Î»y.Î»y1.x y y1 must be renamed, but it cannot choose to use
      # [y := y1] because it would make the inner y bind to the second
      # abstraction instead of the first one.
      assertReduce '(Î»x.Î»y.Î»y1.x y) y', 'Î»y2.Î»y1.y y2'
      # Same thing as above but with higher numbers.
      assertReduce '(Î»x.Î»y4.Î»y5.x y4) y4', 'Î»y6.Î»y5.y4 y6'
      # Same idea, but in this case it has to go quite deep to find a new name.
      assertReduce '(Î»x.Î»y.Î»y1.Î»y2.Î»y3.Î»y4.x y) y', 'Î»y5.Î»y1.Î»y2.Î»y3.Î»y4.y y5'

    it 'does not choose a name that collides with free variables', ->
      assertReduce '(Î»x.Î»y.x y y1) y', 'Î»y2.y y2 y1'

    it 'does not choose a name that collides in any form', ->
      # This test combines the other three criteria. y can't be renamed to y1 or
      # y4 because it would collide with free variables; nor renamed to y2 or y5
      # because it would make y bind to an inner abstraction; not renamed to y3
      # or y6 because they are free in the substitution term.
      assertReduce '(Î»x.Î»y.Î»y2.Î»y5.x y y1 y4) (y y3 y6)', 'Î»y7.Î»y2.Î»y5.y y3 y6 y7 y1 y4'

# TODO Add a test of a beta-reduction that requires more than one
# alpha-conversion, like (Î»x.(Î»y.x)(Î»z.x)) (y z)
# or (Î»x.Î»y.x (Î»z.x)) (y z)
# TODO Add multi-step reduction tests!
