assert = require 'assert'
{Var, App, Fun, parse, termStr, reduceProgram} = require '../src/lambda'

parseTerm = (str) ->
  {terms, defs} = parse str
  assert.equal 1, terms.length
  assert.deepEqual {}, defs
  terms[0]

assertParse = (str, expected) ->
  term = parseTerm str
  assert.deepEqual term, expected

describe 'parse()', ->
  
  it 'parses single variables', ->
    assertParse 'x', Var 'x'
    assertParse 'foo42_bar-baz', Var 'foo42_bar-baz'

  it 'parses applications', ->
    assertParse 'a b', App (Var 'a'), (Var 'b')

  it 'parses lambda abstractions', ->
    assertParse '位x.x', (Fun 'x', (Var 'x'))

  it 'does not parse invalid variables', ->
    # TODO make variables names more flexible so these are all possible!
    assert.throws -> parseTerm 'X'
    assert.throws -> parseTerm '42c'
    assert.throws -> parseTerm 'espa帽ol'
    assert.throws -> parseTerm ''

  it 'does not care about unnecessary parentheses', ->
    assertParse '(x)', Var 'x'
    assertParse '(((x)))', Var 'x'

    assertParse '(x y)', (App (Var 'x'), (Var 'y'))
    assertParse '(x) y', (App (Var 'x'), (Var 'y'))
    assertParse 'x (y)', (App (Var 'x'), (Var 'y'))

    assertParse '(位x.x)', (Fun 'x', (Var 'x'))
    assertParse '位x.(x)', (Fun 'x', (Var 'x'))

  it 'parses nested applications', ->
    assertParse 'x y z',     (App (App (Var 'x'), (Var 'y')), Var 'z')
    assertParse '((x) y) z', (App (App (Var 'x'), (Var 'y')), Var 'z')
    assertParse 'x (y (z))', (App (Var 'x'), (App (Var 'y'), (Var 'z')))

  it 'parses nested abstractions', ->
    assertParse '位x.位y.位z.w',     (Fun 'x', (Fun 'y', (Fun 'z', Var 'w')))
    assertParse '位x.(位y.(位z.w))', (Fun 'x', (Fun 'y', (Fun 'z', Var 'w')))

  it 'does not care about unnecessary whitespace', ->
    assertParse '  x  ', Var 'x'
    assertParse '  x  y  ', (App (Var 'x'), (Var 'y'))
    assertParse '  位x  .  x  ', (Fun 'x', (Var 'x'))

assertTermStr = (str, expected) ->
  term = parseTerm str
  assert.equal (termStr term), expected

describe 'termStr()', ->
  it 'respects operations\' precedence when applying parentheses', ->
    assertTermStr '位a.a 位b.b 位c.c 位d.d', '位a.a 位b.b 位c.c 位d.d'
    assertTermStr '(位a.a) (位b.b) (位c.c) 位d.d', '(位a.a) (位b.b) (位c.c) 位d.d'
    assertTermStr '(((位a.a) 位b.b) 位c.c) 位d.d', '(位a.a) (位b.b) (位c.c) 位d.d'
    assertTermStr '位a.a (位b.b (位c.c (位d.d)))', '位a.a 位b.b 位c.c 位d.d'

reduceTerm = (str, options) ->
  reductions = reduceProgram str, options
  assert.equal 1, reductions.length
  reductions[0]

assertReduce = (expr, expected) ->
  assert.strictEqual (reduceTerm expr).final, expected

describe 'reduceProgram()', ->

  it 'does not reduce simple irreducible expressions', ->
    assertReduce 'x', 'x'
    assertReduce 'x y', 'x y'
    assertReduce '位x.x', '位x.x'

  it 'reduces simple beta-redexes', ->
    assertReduce '(位x.x) y', 'y'
    assertReduce '(位x.x y) z', 'z y'
    assertReduce '(位x.x y) y', 'y y'
    assertReduce '(位x.x x) y', 'y y'

  it 'reduces multiple beta-redex', ->
    assertReduce '(位x.位y.x y) v w', 'v w'

  it 'reduces a beta-redex that is formed only after a reduction', ->
    assertReduce '(位x.x y) (位z.z z)', 'y y'

  it 'does at most maxStep reduction steps', ->
    {totalSteps} = reduceTerm '(位x.x x) (位x.x x)', maxSteps: 42
    assert totalSteps is 42

  it 'indicates when a reduction terminates', ->
    {terminates} = reduceTerm '(位x.x) y'
    assert terminates

  it 'indicates when a reduction does not terminate', ->
    {terminates} = reduceTerm '(位x.x x) (位x.x x)'
    assert not terminates

  describe 'renaming in substitution (位y.T)[x := S]', ->

    it 'renames when y is free in S and x is free in T', ->
      assertReduce '(位x.位y.x y) (y z)', '位y1.y z y1'

    it 'does not rename if y does not occur free in S but x is free in T', ->
      assertReduce '(位x.位y.x y) (w z)', '位y.w z y'

    it 'does not rename if y is free in S but x does not occur in T', ->
      assertReduce '(位x.位y.y) (y z)', '位y.y'

    it 'does not rename if y is free in S but x has only bound occurrences in T', ->
      assertReduce '(位x.位y.y 位x.x) (y z)', '位y.y 位x.x'
      assertReduce '(位x.位y.y 位x.x x y 位x.x) (y z)', '位y.y 位x.x x y 位x.x'

    it 'does not choose a name that makes an inner variable bind to another abstraction', ->
      # In this case, 位y.位y1.x y y1 must be renamed, but it cannot choose to use
      # [y := y1] because it would make the inner y bind to the second
      # abstraction instead of the first one.
      assertReduce '(位x.位y.位y1.x y) y', '位y2.位y1.y y2'
      # Same thing as above but with higher numbers.
      assertReduce '(位x.位y4.位y5.x y4) y4', '位y6.位y5.y4 y6'
      # Same idea, but in this case it has to go quite deep to find a new name.
      assertReduce '(位x.位y.位y1.位y2.位y3.位y4.x y) y', '位y5.位y1.位y2.位y3.位y4.y y5'

    it 'does not choose a name that collides with free variables', ->
      assertReduce '(位x.位y.x y y1) y', '位y2.y y2 y1'

    it 'does not choose a name that collides in any form', ->
      # This test combines the other three criteria. y can't be renamed to y1 or
      # y4 because it would collide with free variables; nor renamed to y2 or y5
      # because it would make y bind to an inner abstraction; not renamed to y3
      # or y6 because they are free in the substitution term.
      assertReduce '(位x.位y.位y2.位y5.x y y1 y4) (y y3 y6)', '位y7.位y2.位y5.y y3 y6 y7 y1 y4'

# TODO Add a test of a beta-reduction that requires more than one
# alpha-conversion, like (位x.(位y.x)(位z.x)) (y z)
# or (位x.位y.x (位z.x)) (y z)
# TODO Add multi-step reduction tests!
